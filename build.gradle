plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.3.1'
}

application {
    mainClass = 'com.f9n.altibase.exporter.Main'
}

group = 'com.f9n'
version = '0.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

repositories {
    mavenCentral()
}

dependencies {
    // Altibase
    implementation 'com.altibase:altibase-jdbc:7.1.0.9.2'

    // Prometheus (BOM aligns versions; omit version for BOM-managed artifacts)
    implementation platform('io.prometheus:prometheus-metrics-bom:1.4.3')
    implementation 'io.prometheus:prometheus-metrics-core'
    implementation 'io.prometheus:prometheus-metrics-exporter-httpserver'
    implementation 'io.prometheus:prometheus-metrics-instrumentation-jvm'

    // Config
    implementation 'org.yaml:snakeyaml:2.2'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.9'
    implementation 'net.logstash.logback:logstash-logback-encoder:9.0'

    // Test
    testImplementation platform('org.junit:junit-bom:5.11.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

shadowJar {
    archiveBaseName.set('altibase-exporter')
    archiveClassifier.set('')
    archiveVersion.set('')
    manifest {
        attributes 'Main-Class': 'com.f9n.altibase.exporter.Main',
                   'Implementation-Version': project.version
    }
    mergeServiceFiles()
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}

tasks.named('build') {
    dependsOn shadowJar
}

tasks.named('jar') {
    enabled = false
}

tasks.named('test', Test) {
    useJUnitPlatform()
}
